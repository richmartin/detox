apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"

repositories { mavenCentral() }

dependencies {
  testCompile(
      "com.google.guava:guava:15.0",
      "com.google.code.findbugs:jsr305:2.0.2",
      "javax.mail:mail:1.4.1",
      "junit:junit:4.11"
  )
}

javadoc {
  excludes = ["**/org/json/**"];
}

task javadocJar(type: Jar) {
  classifier = "javadoc"
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives jar, javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

group = "com.moozvine"
archivesBaseName = "detox"
version = "1.0.1"

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: "xxx", password: "xxx")
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: "xxxx", password: "xxxx")
      }

      pom.project {
        name 'Moozvine Detox'
        packaging 'jar'
        description 'Java Data Transfer & Value Objects'
        url 'https://github.com/richmartin/detox'

        licenses {
          license {
            name 'GNU General Public License v2.0'
            url 'http://www.gnu.org/licenses/gpl-2.0.txt'
          }
        }

        scm {
          connection 'scm:git:git://github.com/richmartin/detox.git'
          developerConnection 'scm:git:git://github.com/richmartin/detox.git'
          url 'https://github.com/richmartin/detox'
        }

        developers {
          developer {
            id 'richmartin'
            name 'Rich Martin'
            email 'detox@moozvine.com'
          }
        }
      }
    }
  }
}